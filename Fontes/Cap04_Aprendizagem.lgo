APRENDA ARCO1 :TAMANHO :GRAU
  REPITA :GRAU / 10 [
    PARAFRENTE :TAMANHO
    PARADIREITA 10
  ]
FIM

APRENDA BOCA
  USENADA
  PF 20 PD 90 PF 25
  USELÁPIS
  PF 50
  USENADA
  PT 75
FIM

APRENDA CABECA
  USELÁPIS
  REPITA 4 [PF 100 PD 90]
  USENADA
FIM

APRENDA CONTAGEM1
  ATRIBUA "I 1 
  ENQUANTO [:I <= 5] [
    ESCREVA :I
    ATRIBUA "I :I + 1 
  ]
FIM

APRENDA CONTAGEM2
  ATRIBUA "I 1 
  FAÇA.ATÉ [
    ESCREVA :I
    ATRIBUA "I :I + 1 
  ][:I > 5]
FIM

APRENDA CONTAGEM3
  FAÇAPARA [I 1 5 1] [
    ESCREVA :I
  ]
FIM

APRENDA ESTRELA40
  FAÇAPARA [I 1 40] [
    PT 200 
    PE 170
  ]
FIM

APRENDA FACE
  CABECA
  BOCA
  PREPARA.OLHO
  OLHO.ESQUERDO
  OLHO.DIREITO
  NARIZ
FIM

APRENDA FRACTAL1 :LADO
  REPITA 6 [
    PD 120
    PONTA :LADO
    PE  60
  ]
FIM

APRENDA FRACTAL2 :LADO
  REPITA 6 [
    PE 120
    PONTA :LADO
    PD  60
  ]
FIM

APRENDA FRACTAL3 :LADO
  PD 90
  REPITA 2 [
    PONTA :LADO
    PE  60
    PONTA :LADO
    PD 120
  ]
FIM

APRENDA FRACTAL4 :LADO
  REPITA 4 [
    FRACTAL3 :LADO
    PE 120
    PD  30
  ]
FIM

APRENDA IDADE
  ATRIBUA "VLR LEIAP
  DEVOLVA (SE :VLR >= 18 [ESC [Maior de idade]] [ESC [Menor de ida-de]])
FIM

APRENDA LARANJA
  REPITA 4 [PF 50 PD 90]
  USENADA
  MUDEPOS  [30 30]
  MUDECP   [255 163 0]
  PINTE 
  MUDEPOS  [0 0]
  MUDECL   [0 0 0]
  USELÁPIS
FIM

APRENDA MALUQUINHO
  REPITA 100 [
    PF SORTEIENÚMERO  60
    PD SORTEIENÚMERO 360
  ]
FIM

APRENDA NARIZ
  PD 90 PF 20 PE 45
  USELÁPIS
  REPITA 4 [PF 29 PD 90]
  USENADA
  CENTRO
  PE 135
  USELÁPIS
FIM

APRENDA OLHO
  USELÁPIS
  REPITA 4 [PF 15 PD 90]
FIM

APRENDA OLHO.DIREITO
  USELÁPIS
  OLHO
  USENADA
  PT 15
FIM

APRENDA OLHO.ESQUERDO
  USELÁPIS
  OLHO
  USENADA
  PF 45
FIM

APRENDA PENTAGRAMA
  MUDEDIREÇÃO 18
  ATRIBUA "I 1 
  ENQUANTO [:I <= 5] [
    PF 100
    PD 144 
    ATRIBUA "I :I + 1
  ]
FIM

APRENDA POLIGOM :LADOS :TAMANHO
  OT
  SE :LADOS = 30 [PARE]
  USELÁPIS
  REPITA :LADOS [PF :TAMANHO PD 360 / :LADOS] 
  USENADA
  CENTRO
  USELÁPIS
  REPITA :LADOS [PF :TAMANHO PE 360 / :LADOS] 
  ESPERE 30
  POLIGOM (:LADOS + 1) :TAMANHO
  MT
FIM

APRENDA PONTA :LADO
  PF :LADO PE  60
  PF :LADO PD 120
  PF :LADO PE  60
  PF :LADO
FIM

APRENDA PREPARA.OLHO
  PE 90 PF 65 PD 90 PF 20 
FIM

APRENDA RAIO :TAMANHO
  REPITA 2 [
    ARCO2 :TAMANHO 90
    ARCO1 :TAMANHO 90
  ]
FIM

APRENDA RODAFLOR
  FAÇAPARA [I 1 6] [ 
    ATRIBUA "I 1 
    ENQUANTO [:I <= 120] [
      PF 1 PD 1
      ATRIBUA "I :I + 1
    ]
    PD 60 
    REPITA 180 [
      PF 1 PD 1
    ] 
    PD 60
  ]
FIM

APRENDA SAMAMBAIA :FOLHAS
  SE :FOLHAS > 4 [
    PF :FOLHAS / 25 
    PE 80 SAMAMBAIA :FOLHAS * 0.3
    PD 82  PF :FOLHAS / 25 
    PD 80 SAMAMBAIA :FOLHAS * 0.3
    PE 78 SAMAMBAIA :FOLHAS * 0.9
    PE  2 PT  :FOLHAS / 25
    PE  2 PT  :FOLHAS / 25
  ]
FIM

APRENDA SOL :TAMANHO
  MUDECF [0 0 128]
  MUDECL [255 165 0]
  REPITA 9 [
    RAIO :TAMANHO
    PARADIREITA 160
  ]
FIM

APRENDA TABUADA
  ATRIBUA "NUMERO LEIAP
  SESENÃO (E :NUMERO >= 2 :NUMERO <= 9) [
    REPITA 10 [ESC (
      FR :NUMERO "X CONTEVEZES "= :NUMERO * CONTEVEZES
    )]
  ][
    ESC [Valor fora da faixa]
  ]
FIM

APRENDA TABUADA2
  ATRIBUA "NUMERO LEIAP
  SESENÃO (E :NUMERO >= 2 :NUMERO <= 9) [
    REPITA 10 [ESC (
      FR 
        FORMATONÚMERO :NUMERO 2 0 
      "X 
        FORMATONÚMERO CONTEVEZES 2 0
      "= 
        FORMATONÚMERO (:NUMERO * CONTEVEZES) 3 0
    )]
  ][
    ESC [Valor fora da faixa]
  ]
FIM

atribua "i 6
atribua "numero 4
atribua "valor 44
